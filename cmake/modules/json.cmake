option(ENABLE_JSON "Enables support for JSON files." OFF)
if (ENABLE_JSON)
    message(STATUS "Enabling JSON support.")
    option(FIND_PACKAGE_JSON "Uses find_package to find nlohmann/json locally instead of downloading it from github." OFF)
    if (ENABLE_ADIOS2)
        # the adios dependency already adds json, so link to that instead
        set(jsonLibName "nlohmann_json")
    else()
        if (FIND_PACKAGE_JSON)
            find_package(nlohmann_json 3.12.0 REQUIRED)
        else ()
            include(FetchContent)
            set (jsonReleaseLoc https://github.com/nlohmann/json/releases/download/v3.12.0/json.tar.xz)
            FetchContent_Declare(json URL ${jsonReleaseLoc})
            FetchContent_MakeAvailable(json)
        endif()
        set(jsonLibName "nlohmann_json::nlohmann_json")
    endif()
    set(JSON_COMPILE_DEFINITION "ENABLE_JSON")
    set(JSON_LIB ${jsonLibName})
else ()
    set(JSON_LIB "")
endif()
