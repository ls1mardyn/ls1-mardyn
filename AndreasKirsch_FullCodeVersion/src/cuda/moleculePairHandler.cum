#ifndef MOLECULEPAIRHANDLER_CUM__
#define MOLECULEPAIRHANDLER_CUM__

#include "potForce.cum"

#include "globalStats.cum"

__constant__ __device__ floatType cutOffRadiusSquared;
__constant__ __device__ floatType epsRFInvrc3;

namespace MoleculePairHandler {
	template<typename MoleculeA, typename MoleculeB>
	__device__ void process( int threadIndex, MoleculeA & __restrict__ moleculeA, MoleculeB & __restrict__ moleculeB ) {
		const floatType3 distanceAtoB = moleculeB.getPosition() - moleculeA.getPosition();
		const floatType distanceSquared = dot( distanceAtoB, distanceAtoB );

		if( distanceSquared >= cutOffRadiusSquared ) {
			return;
		}

		const ComponentDescriptor &descriptorA = componentDescriptors[moleculeA.getComponentType()];
		const ComponentDescriptor &descriptorB = componentDescriptors[moleculeB.getComponentType()];
#if MAX_NUM_LJCENTERS > 0
		processLJCenters( threadIndex, moleculeA, moleculeB, descriptorA, descriptorB, distanceAtoB );
#endif
#if MAX_NUM_CHARGES > 0
		processCharges( threadIndex, moleculeA, moleculeB, descriptorA, descriptorB, distanceAtoB );
#endif
#if MAX_NUM_DIPOLES > 0
		processDipoles( threadIndex, moleculeA, moleculeB, descriptorA, descriptorB, distanceAtoB );
#endif
#if MAX_NUM_DIPOLES > 0 && MAX_NUM_CHARGES > 0
		processDipolesCharges( threadIndex, moleculeA, moleculeB, descriptorA, descriptorB, distanceAtoB );
		processDipolesCharges( threadIndex, moleculeB, moleculeA, descriptorB, descriptorA, -distanceAtoB );
#endif
	}

	template<typename MoleculeA, typename MoleculeB>
	__device__ void processLJCenters( int threadIndex,
			MoleculeA & __restrict__ moleculeA, MoleculeB & __restrict__ moleculeB,
			const ComponentDescriptor & __restrict__ descriptorA, const ComponentDescriptor & __restrict__ descriptorB,
			const floatType3 & __restrict__ distanceAtoB
		) {

		const ComponentType componentTypeA = moleculeA.getComponentType();
		const ComponentType componentTypeB = moleculeB.getComponentType();
		const floatType xi = componentMixXis[ componentTypeA ][ componentTypeB ];
		const floatType eta = componentMixEtas[ componentTypeA ][ componentTypeB ];

		for( int ljIndexA = 0 ; ljIndexA < MAX_NUM_LJCENTERS ; ljIndexA++ ) {
			if( ljIndexA >= descriptorA.numLJCenters ) {
				continue;
			}

			const ComponentDescriptor::LJCenter &ljCenterA = descriptorA.ljCenters[ljIndexA];
			const floatType3 relativePositionA = moleculeA.getRotation() * ljCenterA.relativePosition;

			for( int ljIndexB = 0 ; ljIndexB < MAX_NUM_LJCENTERS ; ljIndexB++ ) {
				if( ljIndexB >= descriptorB.numLJCenters ) {
					continue;
				}

				const ComponentDescriptor::LJCenter &ljCenterB = descriptorB.ljCenters[ljIndexB];
				const floatType3 relativePositionB = moleculeB.getRotation() * ljCenterB.relativePosition;

				LJParameters mixedParameters;
				mixedParameters.epsilon = xi * sqrt( ljCenterA.ljParameters.epsilon * ljCenterB.ljParameters.epsilon );
				mixedParameters.sigma = eta * 0.5f * (ljCenterA.ljParameters.sigma + ljCenterB.ljParameters.sigma);

				const floatType3 totalDistanceAtoB = distanceAtoB + relativePositionB - relativePositionA;

				floatType3 forceAtoB;
				floatType potential;
				calculateLennardJones( totalDistanceAtoB, mixedParameters, forceAtoB, potential );

				moleculeA.addForce( forceAtoB );
				moleculeB.addForce( -forceAtoB );

				moleculeA.addTorque( cross( relativePositionA, forceAtoB ) );
				// -cross( A, B ) = cross( B, A )
				moleculeB.addTorque( cross( forceAtoB, relativePositionB ) );

				const floatType virial = dot( distanceAtoB, forceAtoB );

				ThreadBlockCellStats::add( threadIndex, potential, virial );
			}
		}
	}

#if MAX_NUM_CHARGES > 0
	template<typename MoleculeA, typename MoleculeB>
	__device__ void processCharges( int threadIndex,
			MoleculeA & __restrict__ moleculeA, MoleculeB & __restrict__ moleculeB,
			const ComponentDescriptor & __restrict__ descriptorA, const ComponentDescriptor & __restrict__ descriptorB,
			const floatType3 & __restrict__ distanceAtoB
		) {
		for( int chargeIndexA = 0 ; chargeIndexA < MAX_NUM_CHARGES ; chargeIndexA++ ) {
			if( chargeIndexA >= descriptorA.numCharges ) {
				continue;
			}

			const ComponentDescriptor::Charge &chargeA = descriptorA.charges[chargeIndexA];
			const floatType3 relativePositionA = moleculeA.getRotation() * chargeA.relativePosition;

			for( int chargeIndexB = 0 ; chargeIndexB < MAX_NUM_CHARGES ; chargeIndexB++ ) {
				if( chargeIndexB >= descriptorB.numCharges ) {
					continue;
				}

				const ComponentDescriptor::Charge &chargeB = descriptorB.charges[chargeIndexB];
				const floatType3 relativePositionB = moleculeB.getRotation() * chargeB.relativePosition;

				const floatType3 totalDistanceAtoB = distanceAtoB + relativePositionB - relativePositionA;
				const floatType totalDistanceSquared = dot( totalDistanceAtoB, totalDistanceAtoB );

				floatType3 forceAtoB;
				floatType potential;

				const floatType Q1Q2Per4PiE0 = chargeA.q * chargeB.q;
				calculateCharges( totalDistanceAtoB, totalDistanceSquared, Q1Q2Per4PiE0, forceAtoB, potential );

				moleculeA.addForce( forceAtoB );
				moleculeB.addForce( -forceAtoB );

				moleculeA.addTorque( cross( relativePositionA, forceAtoB ) );
				moleculeB.addTorque( cross( forceAtoB, relativePositionB ) );

				const floatType virial = dot( distanceAtoB, forceAtoB );

				ThreadBlockCellStats::add( threadIndex, potential, virial );
			}
		}
	}
#endif

#if MAX_NUM_DIPOLES > 0
	template<typename MoleculeA, typename MoleculeB>
	__device__ void processDipoles( int threadIndex,
				MoleculeA & __restrict__ moleculeA, MoleculeB & __restrict__ moleculeB,
				const ComponentDescriptor & __restrict__ descriptorA, const ComponentDescriptor & __restrict__ descriptorB,
				const floatType3 & __restrict__ distanceAtoB
			) {
		for( int dipoleIndexA = 0 ; dipoleIndexA < MAX_NUM_DIPOLES ; dipoleIndexA++ ) {
			if( dipoleIndexA >= descriptorA.numDipoles ) {
				continue;
			}

			const ComponentDescriptor::Dipole &dipoleA = descriptorA.dipoles[dipoleIndexA];
			const Matrix3x3 rotationA = moleculeA.getRotation();
			const floatType3 relativePositionA = rotationA * dipoleA.relativePosition;
			const floatType3 eA = rotationA * dipoleA.relativeE;

			for( int dipoleIndexB = 0 ; dipoleIndexB < MAX_NUM_DIPOLES ; dipoleIndexB++ ) {
				if( dipoleIndexB >= descriptorB.numDipoles ) {
					continue;
				}

				const ComponentDescriptor::Dipole &dipoleB = descriptorB.dipoles[dipoleIndexB];
				const Matrix3x3 rotationB = moleculeB.getRotation();
				const floatType3 relativePositionB = rotationB * dipoleB.relativePosition;
				const floatType3 eB = rotationB * dipoleB.relativeE;

				const floatType3 totalDistanceAtoB = distanceAtoB + relativePositionB - relativePositionA;
				const floatType totalDistanceSquared = dot( totalDistanceAtoB, totalDistanceAtoB );

				floatType3 forceAtoB;
				floatType potential;
				floatType myRF;
				floatType3 torqueA;
				floatType3 torqueB;

				const floatType my2 = dipoleA.absMu * dipoleB.absMu;
				const floatType rffac = my2 * epsRFInvrc3;
				calculateDipoleDipole( totalDistanceAtoB, totalDistanceSquared,
						eA, eB, my2, rffac, forceAtoB, potential, myRF, torqueA, torqueB );

				moleculeA.addForce( forceAtoB );
				moleculeB.addForce( -forceAtoB );

				moleculeA.addTorque( torqueA );
				moleculeB.addTorque( torqueB );

				const floatType virial = dot( distanceAtoB, forceAtoB );

				// NOTE: virial sign, so its [..] - 3. * [..] and not [..] + 3. * [..]
				ThreadBlockCellStats::add( threadIndex, potential + myRF, virial - 3. * myRF);
			}
		}
	}
#endif

#if MAX_NUM_DIPOLES > 0 && MAX_NUM_CHARGES > 0
	template<typename MoleculeA, typename MoleculeB>
	__device__ void processDipolesCharges( int threadIndex,
				MoleculeA & __restrict__ moleculeA, MoleculeB & __restrict__ moleculeB,
				const ComponentDescriptor & __restrict__ descriptorA, const ComponentDescriptor & __restrict__ descriptorB,
				const floatType3 & __restrict__ distanceAtoB
			) {
		// dipole as outer loop because charges are more light-weight (no rotation of e)
		for( int dipoleIndexA = 0 ; dipoleIndexA < MAX_NUM_DIPOLES ; dipoleIndexA++ ) {
			if( dipoleIndexA >= descriptorA.numDipoles ) {
				continue;
			}

			const ComponentDescriptor::Dipole &dipoleA = descriptorA.dipoles[dipoleIndexA];
			const Matrix3x3 rotationA = moleculeA.getRotation();
			const floatType3 relativePositionA = rotationA * dipoleA.relativePosition;
			const floatType3 eA = rotationA * dipoleA.relativeE;

			for( int chargeIndexB = 0 ; chargeIndexB < MAX_NUM_CHARGES ; chargeIndexB++ ) {
				if( chargeIndexB >= descriptorB.numCharges ) {
					continue;
				}

				const ComponentDescriptor::Charge &chargeB = descriptorB.charges[chargeIndexB];
				const floatType3 relativePositionB = moleculeB.getRotation() * chargeB.relativePosition;

				const floatType3 totalDistanceAtoB = distanceAtoB + relativePositionB - relativePositionA;
				const floatType totalDistanceSquared = dot( totalDistanceAtoB, totalDistanceAtoB );

				floatType3 forceAtoB;
				floatType potential;
				floatType3 torqueA;

				const floatType qMyPer4PiE0 = dipoleA.absMu * chargeB.q;
				calculateDipoleCharge( totalDistanceAtoB, totalDistanceSquared, eA, qMyPer4PiE0, forceAtoB, potential, torqueA );

				moleculeA.addForce( forceAtoB );
				moleculeB.addForce( -forceAtoB );

				moleculeA.addTorque( torqueA );

				const floatType virial = dot( distanceAtoB, forceAtoB );

				ThreadBlockCellStats::add( threadIndex, potential, virial );
			}
		}
	}
#endif
};

#endif
